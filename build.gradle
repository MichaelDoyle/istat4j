apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

defaultTasks 'clean', 'build'

sourceCompatibility = 1.6
group = 'com.github.michaeldoyle'
version = '1.0.0-SNAPSHOT'

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.simpleframework:simple-xml:2.7.1'
	compile 'org.slf4j:slf4j-api:1.7.5'
	testCompile 'junit:junit:4.10'
	testCompile 'ch.qos.logback:logback-classic:1.0.13'
	testCompile 'ch.qos.logback:logback-core:1.0.13'
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.9'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			
			repository(url:  "${project.version.endsWith('-SNAPSHOT') ? 'https://oss.sonatype.org/content/repositories/snapshots/' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2' }") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            
            pom.project {
               name 'istat4j'
               packaging 'jar'
               description 'A Java client library for querying the iStat Server.'
               url 'https://github.com/MichaelDoyle/istat4j'
               scm {
                   url 'scm:git@github.com:MichaelDoyle/istat4j.git'
                   connection 'scm:git@github.com:MichaelDoyle/istat4j.git'
                   developerConnection 'scm:git@github.com:MichaelDoyle/istat4j.git'
               }

               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'michaeldoyle'
                       name 'Michael Doyle'
                   }
               }
           }
        }
    }
}

